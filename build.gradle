plugins {
	id 'fabric-loom'
	id 'maven-publish'
}

archivesBaseName = project.archives_base_name
version = "${rootProject.mod_version}+MC${rootProject.minecraft_version}"
group = project.maven_group

ext.polarCommonRepos = {
	maven {
	    name = "Ladysnake Libs"
	    url = "https://ladysnake.jfrog.io/artifactory/mods"
	    content {
	        includeGroup 'dev.onyxstudios.cardinal-components-api'
	    }
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
}
ext.polarCommonDeps = {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"
    modApi "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${rootProject.cardinal_components_version}"
}
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories ext.polarCommonRepos
repositories {
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
        content {
            includeGroup 'dev.emi'
        }
    }
    maven {
        name 'JitPack'
        url 'https://jitpack.io'
        content {
            includeGroup 'com.github.Daomephsta'
        }
    }
}

dependencies ext.polarCommonDeps
dependencies {
    implementation project(path: 'polar-api', configuration: 'namedElements')
    modImplementation("dev.emi:trinkets:${project.trinkets_version}") {
		exclude group: 'dev.onyxstudios.cardinal-components-api'
    }
    implementation project(path: ':Inscribe', configuration: 'developmentElements')
    // Only include implementation detail dependencies to reduce JAR size)
    include project(path: ':Inscribe')
    include(modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cardinal_components_version}"))
    include(modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-item:${project.cardinal_components_version}"))
    include(modImplementation("me.zeroeightsix:fiber:${project.fiber_version}"))
    include(modRuntimeOnly('com.github.Daomephsta:modifier-tooltip-fix:v1.0.2'))
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
    from 'LICENSE'
    from project('polar-api').sourceSets.main.output
    from 'CREDITS.md'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}