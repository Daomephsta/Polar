// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

ext.secrets = new Properties()
file('secrets.properties').withReader {secrets.load(it)}

configurations {
	//Jars to package using ContainedDeps
	containedDep {
		transitive = false
	}
	compile.extendsFrom containedDep
}

allprojects {
	apply plugin: 'net.minecraftforge.gradle.forge'

	minecraft {
		version = project.mcVersion + "-" + project.forgeVersion
		runDir = "run"
		mappings = project.mappingsVer
		makeObfSourceJar = false
	}
    println extensions.findByName('minecraft').clientRunArgs
    
    eclipse {
        classpath {
            file {
                whenMerged {
                    def libs = entries.findAll {it.path.contains 'file-deps'}
                    libs.each {lib ->
                        def libPath = file(lib.path)
                        def nameNoExt = libPath.name.replaceFirst(~/\.[^\.]+$/, '')
                        
                        def sources = file("${libPath.parent}/${nameNoExt}-sources.jar")
                        if (sources.exists())
                        {
                            lib.sourcePath = fileReference(sources)
                            println "${sources} attached as source for ${libPath}"
                        }
                    }
                }
            }
        }
    }
	
	repositories {
		maven 
		{
			name 'JEI'
			url 'http://dvs1.progwml6.com/files/maven'
		}
		maven 
		{ 
			name 'Patchouli'
			url 'https://maven.blamejared.com' 
		}
		mavenLocal()
	}

	dependencies {
		// compile against the JEI API
		deobfCompile "mezz.jei:jei_${project.mcVersion}:${project.jeiVersion}:api"
		// at runtime, use the full JEI jar
		runtime "mezz.jei:jei_${project.mcVersion}:${project.jeiVersion}"
        deobfCompile "vazkii.patchouli:Patchouli:${project.patchouliVersion}"
	}
}

version = "${rootProject.mcVersion}-${rootProject.modVersion}"
group= "leviathan143.polar" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Polar"

repositories {
	maven
	{
		name 'MMD'
		url 'https://maven.mcmoddev.com'
	}
}

dependencies {
	containedDep "daomephsta.umbra:umbra-minecraft:${project.umbraMCVersion}"
	containedDep "daomephsta.umbra:umbra-java:${project.umbraJavaVersion}"
	compile project('polar-api')
	// compile against the Baubles API
	deobfCompile "com.azanor:Baubles:${project.baublesVersion}:api"
  	// at runtime, use the full Baubles jar
  	runtime "com.azanor:Baubles:${project.baublesVersion}"
}

minecraft {
    clientRunArgs = ["--uuid ${secrets.uuid}", "--username ${secrets.username}"]
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task generateDependencyMetaFiles {doLast{
	//Delete any previous meta files
	file("${buildDir}/dependencyMeta").deleteDir()
	//Generate new meta files
	configurations.containedDep.resolvedConfiguration.resolvedArtifacts.each {
		def metaFile = file("${buildDir}/dependencyMeta/${it.file.name}.meta")
		metaFile.parentFile.mkdirs()
		//The newline is very important
		metaFile.text = "Maven-Artifact: ${it.moduleVersion.id}\n"
	}
}}

jar {
	from project('polar-api').sourceSets.main.output
	//Package contained dependencies
	into('/META-INF/libraries') {
		from configurations.containedDep, file("${buildDir}/dependencyMeta")
	}
	//Add contained dependencies to the manifest
	manifest {
		attributes 'ContainedDeps': configurations.containedDep.collect {it.name}.join(' ')
	}
    //Add CREDITS.md
    into('/') {
        from('CREDITS.md')
    }
	
	dependsOn generateDependencyMetaFiles
}
